/*
 * Copyright 2015-2018 Canoo Engineering AG.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'application'

mainClassName = 'com.canoo.dolphin.webdeployment.client.ClientApplication'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    compile project(':web-deployment-common')
    compile project(':dolphin-platform-remoting-client-javafx')
}

jar {
    archiveName = 'fxclient.jar'
    doFirst {
        def serviceDir = file("$buildDir/META-INF/services")
        serviceDir.deleteDir()
        serviceDir.mkdirs()

        // copy all service files from deps to buildDir
        for(file in configurations.runtime) {
            zipTree(file).matching{ include 'META-INF/services/*' }.each { f ->
                new File(serviceDir, f.name) << f.getText("UTF-8")+"\n"
            }
        }
    }

    from(configurations.runtime.collect{ it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/**' // Don't let Gradle merge service files
    }
    from fileTree(buildDir).matching{ include 'META-INF/services/*' }
}

jar.doLast {
    def libFiles = files { file('build/libs').listFiles() }
    libFiles.each {

        ant.jar(destfile: it, update: true) {
            delegate.manifest {
                attribute(name: 'permissions', value: 'all-permissions')
                attribute(name: 'codebase', value: '*')
            }
        }

        ant.signjar(
                alias: "test",
                jar: it,
                keystore: "keystore/test.keystore",
                storepass: "123456",
                keypass: "123456",
                destDir: 'fxclient',
                preservelastmodified: 'true'
        )
    }
}

build.dependsOn jar
